plugins {
    alias(libs.plugins.android.library)
    id 'maven-publish'
}

android {
    namespace 'com.saucelabs.visual'
    compileSdk 34

    defaultConfig {
        minSdk 21
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "String", "SAUCE_USERNAME", getProperty("SAUCE_USERNAME", "\"${System.getenv('SAUCE_USERNAME')}\"")
        buildConfigField "String", "SAUCE_ACCESS_KEY", getProperty("SAUCE_ACCESS_KEY", "\"${System.getenv('SAUCE_ACCESS_KEY')}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    publishing {
        singleVariant("release") {
        }
    }
    buildFeatures {
        buildConfig = true
    }
}

String getProperty(String name, String defaultValue) {
    if (!rootProject.file("./local.properties").exists()) {
        return defaultValue
    }
    def properties = new Properties()
    properties.load(rootProject.file("./local.properties").newDataInputStream())
    return properties.containsKey(name) ? properties[name] : defaultValue
}

dependencies {
    implementation libs.httpclient
    implementation libs.jackson.databind
    implementation libs.appcompat
    implementation libs.material
    implementation libs.monitor
    implementation libs.junit
    implementation (libs.visualJava) {
        exclude group: 'com.graphql-java'
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'org.slf4j'
    }
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.saucelabs.visual'
            artifactId = 'visual-espresso'
            version = '0.0.1'
            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

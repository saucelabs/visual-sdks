plugins {
    alias libs.plugins.android.library
    alias libs.plugins.apollographql
    id 'maven-publish'
    id 'signing'
}

android {
    namespace rootProject.group
    compileSdk 34

    defaultConfig {
        minSdk 21
        versionCode 1
        versionName rootProject.version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "String", "SAUCE_USERNAME", "\"${getProperty("SAUCE_USERNAME", "")}\""
        buildConfigField "String", "SAUCE_ACCESS_KEY", "\"${getProperty("SAUCE_ACCESS_KEY", "")}\""
        buildConfigField "String", "VERSION_NAME", "\"${android.defaultConfig.versionName}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
    buildFeatures {
        buildConfig = true
    }
}

String getProperty(String name, String defaultValue) {
    if (rootProject.file("./local.properties").exists()) {
        def properties = new Properties()
        properties.load(rootProject.file("./local.properties").newDataInputStream())
        if (properties.containsKey(name)) {
            return properties[name]
        }
    }
    if (System.getenv(name) != null && !System.getenv(name).isBlank()) {
        return System.getenv(name)
    }
    return defaultValue
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    implementation libs.junit
    implementation libs.apollo.runtime
    implementation libs.apollo.rx3.support
    implementation libs.espresso.core
    implementation libs.uiautomator
    implementation libs.jsoup
    androidTestImplementation libs.ext.junit
}

ext {
    scmUrl = 'git@github.com:saucelabs/visual-sdks.git'
    artifactName = 'visual-espresso'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = android.namespace
            artifactId = "${artifactName}"
            version = android.defaultConfig.versionName
            afterEvaluate {
                from components.release
            }
            pom {
                name = "${artifactName}"
                description = 'Sauce Visual Espresso SDK'
                url = 'https://github.com/saucelabs/visual-sdks'
                scm {
                    connection = "scm:git:${scmUrl}"
                    developerConnection = "scm:git:${scmUrl}"
                    url = "${scmUrl}"
                }
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://github.com/saucelabs/visual-sdks/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        name = 'Sauce Labs Visual Team'
                        email = 'visual.team@saucelabs.com'
                        organization = 'Sauce Labs'
                        organizationUrl = 'https://saucelabs.com/'
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

apollo {
    service("service") {
        packageName.set("com.saucelabs.visual.graphql")
        generateKotlinModels.set(false)
        warnOnDeprecatedUsages.set(false)
    }
}

signing {
    useInMemoryPgpKeys(
        project.findProperty("signingKey") ?: System.getenv("GPG_PRIVATE_KEY"),
        project.findProperty("signingPassword") ?: System.getenv("GPG_PASSPHRASE")
    )
    sign publishing.publications
}
